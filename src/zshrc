# Put this before powerlevel10k instant prompt
# https://github.com/romkatv/powerlevel10k/issues/702#issuecomment-626222730
emulate zsh -c "$(direnv export zsh)"

# Enable Powerlevel10k instant prompt. Should stay close to the top.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# Load my common shell environment (shared with bash)
source ~/.shell_env

# Lazy-load completion on first Tab press
# IMPORTANT: Must be defined before loading Zim modules to ensure Tab binding persists
# and is not overridden by some plugins.
# This is 1.5x faster than zmodule completion (which does extra dumpfile validation)
# It's 1.1x faster than just doing `autoload -Uz compinit && compinit -C -d "${HOME}/.zcompdump"`
# Has zero startup overhead vs no completion at all
function _lazy_load_completion() {
  # Rebind Tab to default completion widget before initializing
  # This prevents calling this function on subsequent Tab presses
  bindkey '^I' expand-or-complete
  # Shift+Tab for reverse completion
  bindkey '^[[Z' reverse-menu-complete

  # Initialize completion system
  autoload -Uz compinit

  # Validate dumpfile once per day (balances speed vs freshness)
  local dumpfile="${HOME}/.zcompdump"
  if [[ -f "${dumpfile}" && $(date -r "${dumpfile}" +%Y%m%d) != $(date +%Y%m%d) ]]; then
    compinit -d "${dumpfile}"  # Regenerate if older than today
  else
    compinit -C -d "${dumpfile}"  # Skip checks for speed
  fi

  # Compile dumpfile for faster loading (if not already compiled or outdated)
  if [[ ! "${dumpfile}.zwc" -nt "${dumpfile}" ]]; then
    zcompile "${dumpfile}"
  fi

  # Configure completion behavior
  zstyle ':completion:*' menu select
  zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}' 'r:|=*' 'l:|=* r:|=*'
  zstyle ':completion::complete:*' cache-path "${HOME}/.zcompcache"
  zstyle ':completion:*' use-cache on

  # Trigger completion for current command
  zle expand-or-complete
}
zle -N _lazy_load_completion
bindkey '^I' _lazy_load_completion

# Zim: Plugin manager
ZIM_HOME=${ZDOTDIR:-${HOME}}/.zim

# Download zimfw if not installed
if [[ ! -e ${ZIM_HOME}/zimfw.zsh ]]; then
  curl -fsSL --create-dirs -o ${ZIM_HOME}/zimfw.zsh \
      https://github.com/zimfw/zimfw/releases/latest/download/zimfw.zsh
fi

# Install missing modules and update ${ZIM_HOME}/init.zsh if missing or outdated
if [[ ! ${ZIM_HOME}/init.zsh -nt ${ZDOTDIR:-${HOME}}/.zimrc ]]; then
  source ${ZIM_HOME}/zimfw.zsh init -q
fi

# zsh-vi-mode configuration (must be set BEFORE loading the plugin)
ZVM_VI_INSERT_ESCAPE_BINDKEY=jk
ZVM_SYSTEM_CLIPBOARD_ENABLED=true
ZVM_CURSOR_STYLE_ENABLED=false
# FZF integration - must be loaded after zsh-vi-mode to restore keybindings
# zsh-vi-mode resets keybindings, so we need to re-source FZF bindings
function zvm_after_init() {
  # Re-source Zim's FZF keybindings (Ctrl-R, Ctrl-T, Alt-C)
  local zim_fzf_bindings="${ZIM_HOME}/modules/fzf/fzf---zsh.zsh"
  if [[ -f "${zim_fzf_bindings}" ]]; then
    source "${zim_fzf_bindings}"
  fi
}
# Vi mode custom keybindings for normal/visual mode (H, L)
# Must use zvm_after_lazy_keybindings for normal/visual mode bindings
function zvm_after_lazy_keybindings() {
  bindkey -M vicmd 'H' beginning-of-line
  bindkey -M vicmd 'L' end-of-line
}

#
# Configure ohmyzsh tmux plugin
# I just use it to autostart into tmux.
#
ZSH_TMUX_AUTOSTART=true
# Autostart tmux unless DISABLE_TMUX_AUTOSTART is true
# I set DISABLE_TMUX_AUTOSTART in Konsole terminal, so that when I open
# Konsole inside Dolphin, that terminal doesn't have Tmux.
if [ "$DISABLE_TMUX_AUTOSTART" != "true" ]
then
    export ZSH_TMUX_AUTOSTART=true
fi
export ZSH_TMUX_FIXTERM=true
# Dummy compdef for tmux plugin (real one loads with completion on first Tab)
function compdef { : }

# Initialize modules
[[ -f ${ZIM_HOME}/init.zsh ]] && source ${ZIM_HOME}/init.zsh

# Or keep P10k (comment out if using Starship)
[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh

# History settings
HISTSIZE=10000
SAVEHIST=10000
# Share history between sessions
setopt SHARE_HISTORY
# Don't record duplicates
setopt HIST_IGNORE_DUPS
# Don't record commands starting with space
setopt HIST_IGNORE_SPACE

# Globbing settings
# Pass unmatched patterns as-is 
# (mainly to allow me to write something like nix shell nixpkgs#package)
setopt NO_NOMATCH

# Load just completions if available
[[ -f ~/.just.zsh ]] && source ~/.just.zsh

# Direnv integration
if command -v direnv &> /dev/null; then
  eval "$(direnv hook zsh)"
fi
