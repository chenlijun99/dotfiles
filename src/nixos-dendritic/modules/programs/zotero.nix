# Zotero reference manager
{
  flake.modules.homeManager.clj-zotero = {
    config,
    lib,
    pkgs,
    ...
  }:
    with lib; let
      cfg = config.clj.programs.zotero;

      jsonFormat = pkgs.formats.json {};

      zoteroConfigPath = ".zotero/zotero";
      profilesPath = zoteroConfigPath;

      profiles =
        flip mapAttrs' cfg.profiles (_: profile:
          nameValuePair "Profile${toString profile.id}" {
            Name = profile.name;
            Path = profile.path;
            IsRelative = 1;
            Default =
              if profile.isDefault
              then 1
              else 0;
          })
        // {
          General = {StartWithLastProfile = 1;};
        };

      profilesIni = generators.toINI {} profiles;

      userPrefValue = pref:
        builtins.toJSON (
          if isBool pref || isInt pref || isString pref
          then pref
          else builtins.toJSON pref
        );

      mkUserJs = prefs: extraPrefs: ''
        // Generated by Home Manager.
        ${concatStrings (mapAttrsToList (name: value: ''
            user_pref("${name}", ${userPrefValue value});
          '')
          prefs)}
        ${extraPrefs}
      '';
    in {
      options.clj.programs.zotero = {
        enable = mkEnableOption "Zotero";

        package = mkOption {
          type = types.package;
          default = pkgs.zotero;
          description = "The Zotero package to use.";
        };

        profiles = mkOption {
          type = types.attrsOf (types.submodule ({
            config,
            name,
            ...
          }: {
            options = {
              name = mkOption {
                type = types.str;
                default = name;
                description = "Profile name.";
              };

              id = mkOption {
                type = types.ints.unsigned;
                default = 0;
                description = "Profile ID.";
              };

              settings = mkOption {
                type = types.attrsOf (jsonFormat.type
                  // {
                    description = "Zotero preference";
                  });
                default = {};
                description = "Attribute set of Zotero preferences.";
              };

              extraConfig = mkOption {
                type = types.lines;
                default = "";
                description = "Extra preferences to add to user.js.";
              };

              userChrome = mkOption {
                type = types.lines;
                default = "";
                description = "Custom Zotero user chrome CSS.";
              };

              userContent = mkOption {
                type = types.lines;
                default = "";
                description = "Custom Zotero user content CSS.";
              };

              path = mkOption {
                type = types.str;
                default = name;
                description = "Profile path.";
              };

              isDefault = mkOption {
                type = types.bool;
                default = config.id == 0;
                description = "Whether this is a default profile.";
              };
            };
          }));
          default = {};
          description = "Attribute set of Zotero profiles.";
        };
      };

      config = mkIf cfg.enable {
        assertions = [
          (let
            defaults =
              catAttrs "name" (filter (a: a.isDefault) (attrValues cfg.profiles));
          in {
            assertion = cfg.profiles == {} || length defaults == 1;
            message =
              "Must have exactly one default Zotero profile but found "
              + toString (length defaults)
              + optionalString (length defaults > 1)
              (", namely " + concatStringsSep ", " defaults);
          })
        ];

        home.packages = [cfg.package];

        home.file = mkMerge ([
            {
              "${zoteroConfigPath}/profiles.ini" =
                mkIf (cfg.profiles != {}) {text = profilesIni;};
            }
          ]
          ++ flip mapAttrsToList cfg.profiles (_: profile: {
            "${profilesPath}/${profile.path}/.keep".text = "";
            "${profilesPath}/${profile.path}/user.js" = mkIf (profile.settings != {} || profile.extraConfig != "") {
              text = mkUserJs profile.settings profile.extraConfig;
            };
          }));
      };
    };
}
